/*
This file is part of www.nand2tetris.org
and the book "The Elements of Computing Systems"
by Nisan and Schocken, MIT Press.
File name: projects/03/a/PC.hdl
*/

/**
 * A 16-bit counter with load and reset control bits.
 * if      (reset[t] == 1) out[t+1] = 0
 * else if (load[t] == 1)  out[t+1] = in[t]
 * else if (inc[t] == 1)   out[t+1] = out[t] + 1  (integer addition)
 * else                    out[t+1] = out[t]
 */

CHIP PC {
    IN in[16],load,inc,reset;
    OUT out[16];

    PARTS:

    /* we need an incremented version of the output */
    Inc16(in=regout, out=reginc);

    /* we only need to load the register if "load", "inc", or "reset" is true */
    Or(a=load,b=inc,out=w);
    Or(a=w,b=reset,out=loadreg);

    /* first mux: choose between "inc" and "load" */
    Mux16(a=reginc, b=in, sel=load, out=y);

    /* third mux: choose between "inc+load" and "reset" */
    Mux16(a=y, b[0..15]=false, sel=reset, out=z);

    /* not the same "load" as in second mux (if it was, then the
     * register output would only change on load events)
     */
    Register(in=z, load=loadreg, out=regout, out=out);
}
